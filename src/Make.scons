import re
import sys
import os, os.path
import subprocess
import traceback
import pdb
import shutil
import SCons.Util
import platform

Import("*")

execfile("SCons_Build_Errors.py")


AddOption('--CACHEDIR',
    dest='cache_dir',
    type='string',
    default=None,
    action='store',
    help='Specify the SCSons Shared Cache Directory')

cache_dir=GetOption('cache_dir')
if cache_dir == None:
    cache_dir = os.environ.get('EVEREST_SCONS_CACHE_DIR')
if cache_dir != None:
    print('Using Shared Cache Directory %s'%cache_dir)
    CacheDir(cache_dir)

    
    

# construct a default environment
env = Environment()

# --------------------------------------------------------------------
# Configuration of some platform-specific tools

fstar_path = os.path.realpath('../bin/fstar.exe')       # the path to the fstar.exe which may not exist yet
env['FSTAR']   = fstar_path


if sys.platform == 'win32' or sys.platform == 'cygwin':  # Windows 32-bit or 64-bit
    if sys.platform == 'cygwin':
        fd = open('/proc/registry/HKEY_LOCAL_MACHINE/SOFTWARE/Microsoft/MSBuild/ToolsVersions/14.0/MSBuildToolsPath', 'r')
        v = fd.read()
        v = v.rstrip(' \0') # strip the trailing \0 and any trailing whitespace
        fd.close()
    else:
        import _winreg
        k = _winreg.OpenKey(_winreg.HKEY_LOCAL_MACHINE, r'SOFTWARE\Microsoft\MSBuild\ToolsVersions\14.0')
        v = _winreg.QueryValueEx(k, 'MSBuildToolsPath')[0]
        k.Close()
    
    env['FSC']     = 'fsc'
    env['MSBUILD'] = '"'+os.path.join(v, 'MSBuild.exe')+'"'
    uname          = 'Windows_NT'
    env['RUNTIME'] = ''     # no Mono needed to run .NET code (FSLexYacc, etc.)

    # propagate the TMP environment variable into the OS environment block used to launch
    # child processes.  nuget.exe uses TMP as the path to its lock file and falls back
    # on C:\WINDOWS if it doesn't exist, and that is readonly for most Windows users.
    env['ENV']['TMP'] = os.environ['TMP']
    
    # set the OS environment var in child processes.  MSBuild scripts like NuGet.targets consume it.
    env.AppendENVPath('OS', 'Windows_NT')  
else:
    env['FSC']     = 'fsharpc'
    env['MSBUILD'] = 'xbuild'
    uname          = os.uname()[0]  # sysname is at index 0
    env['RUNTIME'] = 'mono'
        
# propagate the OS PATH environment variable into child processes, so tools like menhir can be found  
env['ENV']['PATH']=os.environ['PATH']
env['BIN']=str(Dir('../bin'))
env['CONFIGURATION']='Release'
env['MSBUILDARGS']='/verbosity:minimal /p:Configuration=$CONFIGURATION'
env['NUGET']=File('#/VS/.nuget/NuGet.exe')

print("****************************** $BIN = " + env['BIN'])


Export('env')

# --------------------------------------------------------------------

#top_NET = SConscript('FStar_NET.scons')                                 # vs-fstar
#no_prior, top_LKG = SConscript('ocaml-LKG/ocaml-output.scons')          # boot-ocaml , no prior 
#Ocaml_prior, top_Ocaml = SConscript('ocaml-output/ocaml-output.scons')  # ocaml
#FStar_prior, top_FStar = SConscript('FStar.scons')                      # extract

def Connect_Stages(prior):
    FStar_prior, top_FStar = SConscript('FStar.scons')
    if prior :  env.Depends(FStar_prior, prior)  
    #env.Default(top_FStar)
    env_OCaml  = env.Clone()
    Ocaml_prior, top_Ocaml = SConscript('ocaml-output.scons', exports={'My_env' : env_OCaml}, variant_dir='ocaml-output', duplicate=0)
    env.Depends(Ocaml_prior, top_FStar)
    return top_Ocaml

if 'vs-fstar' in COMMAND_LINE_TARGETS :
    top_NET = SConscript('FStar_NET.scons')
    vs_fstar = env.Alias('vs-fstar', top_NET)
    #env.Default(top_NET)
    pass
elif 'boot-ocaml' in COMMAND_LINE_TARGETS :
    no_prior, top_LKG = SConscript('ocaml-output.scons', exports={'My_env' : env}, variant_dir='ocaml-LKG', duplicate=0)
    boot_ocaml = env.Alias('boot-ocaml', top_LKG)
    #env.Default(top_LKG)
    pass
elif 'extract' in COMMAND_LINE_TARGETS :
    top_FStar = SConscript('FStar.scons')
    #env.Default(top_Fstar)
    pass
elif 'ocaml' in COMMAND_LINE_TARGETS :  # boot, extract, new ocaml
    top = Connect_Stages(None)
    env.Alias('ocaml', top)
    #env.Default(top)
    pass
elif 'all' in COMMAND_LINE_TARGETS :    # LKG ocaml, boot, extract, new ocaml
    top_NET = SConscript('FStar_NET.scons')
    env_LKG = env.Clone()
    no_prior, top_LKG = env_LKG.SConscript('ocaml-output.scons', exports={'My_env' : env_LKG}, variant_dir='ocaml-LKG', duplicate=0)
    if GetOption('clean') :
        top = Connect_Stages(None)
        all = env.Alias('all', [top])
    else:
        top = Connect_Stages(top_LKG)
        all = env.Alias('all', [top_NET, top_LKG, top])

    # This is a compromise. Building SMake all will force build LKG and .NET but cleanup (-c) won't clean it.
    # to clean up LKG you'll still need to invoke SMake boot-ocaml -c
    # to clean up .NET you'll need to invoke SMake vs-fstar -c

    #env.Default(all)
    pass

print("COMMAND_LINE_TARGETS: %s" % COMMAND_LINE_TARGETS)
print("DEFAULT_TARGETS: %s" % [a.name for a in DEFAULT_TARGETS])

# --------------------------------------------------------------------

